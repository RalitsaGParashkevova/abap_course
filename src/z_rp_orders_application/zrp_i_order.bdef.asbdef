managed;
with draft;

define behavior for ZRP_I_ORDER alias Orders
implementation in class zbp_rp_i_order unique
persistent table zrp_orders
draft table ZRP_D_ORDER
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create ( authorization : global );
  update;
  delete;

  field ( numbering : managed, readonly ) OrderUuid;
  field ( readonly ) Status, CreationDate, CompletionDate, CancellationDate, TotalPrice;
  field ( readonly ) LastChangedBy, LastChangedAt, LocalLastChangedAt;
  field ( mandatory ) Customer, DeliveryCountry;
  association _Item { create;with draft; }

  action ( features : instance ) orderCompleted result [1] $self;
  action ( features : instance ) orderCancelled result [1] $self;
  action ( features : instance ) orderInProcess result [1] $self;
  internal action calculateTotalPrice;

  determination setInitialStatus on modify { create; }
  determination incrementOrderId on save { create; }
  determination insertCreationDate on save { create; }


validation orderValidation on save { field ItemCount; create; }
validation customerValidation on save { field Customer; create; }
validation orderName on save { field Name; create; }
validation deliveryCountry on save { field DeliveryCountry; create; }
validation currency on save { field Currency; create; }
validation validateDates on save { field CreationDate, CancellationDate, CompletionDate; create; }


  mapping for zrp_orders
  {
  OrderUUID           =  order_uuid;
  OrderID              =  order_id;
  Name                 = name;
  Status               = status;
  Customer             = customer;
  CreationDate         = creation_date;
  CancellationDate     = cancellation_date;
  CompletionDate       = completion_date;
  DeliveryCountry      = delivery_country;
  TotalPrice           = total_price;
  Currency             = currency;
  LastChangedBy        = last_changed_by;
  LastChangedAt        = last_changed_at;
  LocalLastChangedAt   = local_last_changed_at;


  }
}

define behavior for ZRP_I_ITEM alias Item
implementation in class zbp_rp_i_item unique
persistent table zrp_items
draft table ZRP_D_ITEM
lock dependent by _Order
authorization dependent by _Order
etag master LocalLastChangedAt
{

  update;
  delete;

  association _Order { with draft; }

   field ( numbering : managed, readonly ) ItemUuid;
     field ( readonly ) OrderUuid;
     field ( readonly ) LastChangedBy, LastChangedAt, LocalLastChangedAt;

     mapping for zrp_items
  {
  ItemUUID  = item_uuid;
  OrderUUID = order_uuid;
  Name      = name;
  Price     = price;
  Currency  = currency;
  Quantity  = quantity;
  LastChangedBy = last_changed_by;
  LastChangedAt = last_changed_at;
  LocalLastChangedAt = local_last_changed_at;

  }
}